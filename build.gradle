buildscript {
  group = 'com.github.ptrteixeira.cookbook'
  version = '0.1.0'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
  }
}

plugins {
  id 'application'
  id 'project-report'
  id 'idea'
  id 'org.jetbrains.kotlin.jvm' version '1.1.2-5'
  id 'org.jetbrains.kotlin.kapt' version '1.1.2-5'
}

apply {
  from 'gradle/kotlin.gradle'
  from 'gradle/junit.gradle'
}

mainClassName = "com.github.ptrteixeira.cookbook.CookbookApplicationKt"
distTar.compression = Compression.GZIP

idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}


repositories {
  mavenCentral()
  jcenter()
}

enum Dependency {
  DROPWIZARD('1.1.0'),
  ELASTICSEARCH('5.4.0'),
  DAGGER('2.11'),
  ASSERTJ('3.6.1'),
  MOCKITO('2.8.9'),
  SWAGGER_REQUEST('1.0.11'),
  REST_ASSURED('3.0.2'),
  JACKSON('2.8.7'),
  LOG4J('2.8.1'),
  SLF4J('1.7.24')

  private String version

  private Dependency(String version) { this.version = version }

  String toString() { version }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:${Dependency.DROPWIZARD}"
  compile "org.apache.logging.log4j:log4j-to-slf4j:${Dependency.LOG4J}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${Dependency.JACKSON}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${Dependency.JACKSON}"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${Dependency.JACKSON}"
  compile("org.elasticsearch.client:transport:${Dependency.ELASTICSEARCH}")
  compile "com.google.dagger:dagger:${Dependency.DAGGER}"
  kapt "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"

  testCompile "org.assertj:assertj-core:${Dependency.ASSERTJ}"
  testCompile "io.dropwizard:dropwizard-testing:${Dependency.DROPWIZARD}"
  testCompile "org.mockito:mockito-core:${Dependency.MOCKITO}"
  testCompile "com.atlassian.oai:swagger-request-validator-restassured:${Dependency.SWAGGER_REQUEST}"
  testCompile "io.rest-assured:rest-assured:${Dependency.REST_ASSURED}"
  testCompile "com.google.dagger:dagger:${Dependency.DAGGER}"
  kaptTest "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"
}
