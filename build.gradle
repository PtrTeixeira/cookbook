import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
  group = 'com.github.ptrteixeira.cookbook'
  version = '0.1.0'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
  }
}

plugins {
  id 'application'
  id 'project-report'
  id 'idea'
  id 'org.jetbrains.kotlin.jvm' version '1.1.2-5'
  id 'org.jetbrains.kotlin.kapt' version '1.1.2-5'
}

apply {
  from 'gradle/kotlin.gradle'
  from 'gradle/junit.gradle'
}

mainClassName = "com.github.ptrteixeira.cookbook.CookbookApplicationKt"
distTar.compression = Compression.GZIP

tasks.withType(KotlinCompile).all {
  kotlinOptions {
    jvmTarget = '1.8'
  }
}

idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}


repositories {
  mavenCentral()
  jcenter()
}

enum Dependency {
  ASSERTJ('3.8.0'),
  DAGGER('2.11'),
  DROPWIZARD('1.1.0'),
  ELASTICSEARCH('5.4.0'),
  H2('1.4.196'),
  JACKSON('2.8.9'),
  JDBI('3.0.0-beta2'),
  LOG4J('2.8.2'),
  SLF4J('1.7.25')

  private String version

  private Dependency(String version) { this.version = version }

  String toString() { version }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-jdbi:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-migrations:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-hibernate:${Dependency.DROPWIZARD}"
  compile "org.jdbi:jdbi3-kotlin-sqlobject:${Dependency.JDBI}"
  compile "org.apache.logging.log4j:log4j-to-slf4j:${Dependency.LOG4J}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${Dependency.JACKSON}"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${Dependency.JACKSON}"
  compile "org.elasticsearch.client:transport:${Dependency.ELASTICSEARCH}"
  compile "com.h2database:h2:${Dependency.H2}"
  compile "com.google.dagger:dagger:${Dependency.DAGGER}"
  kapt "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"

  testCompile "org.assertj:assertj-core:${Dependency.ASSERTJ}"
  testCompile "io.dropwizard:dropwizard-testing:${Dependency.DROPWIZARD}"
  testCompile "com.google.dagger:dagger:${Dependency.DAGGER}"
  kaptTest "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"
}
