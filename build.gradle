buildscript {
  group = 'com.github.ptrteixeira.cookbook'
  version = '0.2.2'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC3'
  }
}

plugins {
  id 'application'
  id 'project-report'
  id 'idea'
  id 'nebula.kotlin' version '1.1.4-2'
  id 'org.jetbrains.kotlin.kapt' version '1.1.4-2'
}

apply {
  from 'gradle/kotlin.gradle'
  from 'gradle/junit.gradle'
}

mainClassName = "com.github.ptrteixeira.cookbook.CookbookApplicationKt"
distTar.compression = Compression.GZIP

idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}


repositories {
  mavenCentral()
  jcenter()
}

enum Dependency {
  ASSERTJ('3.8.0'),
  DAGGER('2.11'),
  DROPWIZARD('1.1.4'),
  GOOGLE_API('1.22.0'),
  H2('1.4.196'),
  JACKSON('2.8.10'),
  JDBI('3.0.0-beta2'),
  JERSEY('2.25.1'),
  LOG4J('2.8.2'),
  MOCKITO("2.9.0"),
  SLF4J('1.7.25')

  private String version

  private Dependency(String version) { this.version = version }

  String toString() { version }
}

dependencies {
  compile "io.dropwizard:dropwizard-core:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-jdbi:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-migrations:${Dependency.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-auth:${Dependency.DROPWIZARD}"
  compile "org.jdbi:jdbi3-kotlin-sqlobject:${Dependency.JDBI}"
  compile "org.apache.logging.log4j:log4j-to-slf4j:${Dependency.LOG4J}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${Dependency.JACKSON}"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${Dependency.JACKSON}"
  compile "com.h2database:h2:${Dependency.H2}"
  compile "com.google.dagger:dagger:${Dependency.DAGGER}"
  compile("com.google.api-client:google-api-client:${Dependency.GOOGLE_API}") {
    exclude group: 'com.google.guava', module: 'guava-jdk5'
  }
  kapt "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"

  testCompile "org.assertj:assertj-core:${Dependency.ASSERTJ}"
  testCompile "io.dropwizard:dropwizard-testing:${Dependency.DROPWIZARD}"
  testCompile("org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${Dependency.JERSEY}") {
    exclude group: 'junit', module: 'junit'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
  }
  testCompile "com.google.dagger:dagger:${Dependency.DAGGER}"
  testCompile "org.mockito:mockito-core:${Dependency.MOCKITO}"
  kaptTest "com.google.dagger:dagger-compiler:${Dependency.DAGGER}"
}
