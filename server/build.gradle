buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    classpath 'com.bmuschko:gradle-docker-plugin:3.1.0'
  }
}

plugins {
  id 'application'
  id 'idea'
  id 'nebula.kotlin' version '1.1.50'
  id 'org.jetbrains.kotlin.kapt' version '1.1.50'
  id 'com.diffplug.gradle.spotless' version '3.5.1'
}

apply {
  from "$rootDir/gradle/kotlin.gradle"
  from "$rootDir/gradle/junit.gradle"
  from "$rootDir/gradle/docker.gradle"
}

idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}

spotless {
  kotlin {
    ktlint('0.9.1')
  }
}

mainClassName = "com.github.ptrteixeira.cookbook.CookbookApplicationKt"
distTar.compression = Compression.GZIP

startScripts {
  applicationName = "cookbook"
}

distributions {
  main {
    baseName = 'cookbook'
  }
}

dependencies {
  compile project(':core')
  compile "io.dropwizard:dropwizard-core:${Dependencies.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-jdbi:${Dependencies.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-migrations:${Dependencies.DROPWIZARD}"
  compile "io.dropwizard:dropwizard-auth:${Dependencies.DROPWIZARD}"
  compile "org.jdbi:jdbi3-kotlin-sqlobject:${Dependencies.JDBI}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${Dependencies.JACKSON}"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${Dependencies.JACKSON}"
  compile "com.h2database:h2:${Dependencies.H2}"
  compile "com.google.dagger:dagger:${Dependencies.DAGGER}"
  compile("com.google.api-client:google-api-client:${Dependencies.GOOGLE_API}") {
    exclude group: 'com.google.guava', module: 'guava-jdk5'
  }
  kapt "com.google.dagger:dagger-compiler:${Dependencies.DAGGER}"

  testCompile "org.assertj:assertj-core:${Dependencies.ASSERTJ}"
  testCompile "io.dropwizard:dropwizard-testing:${Dependencies.DROPWIZARD}"
  testCompile("org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${Dependencies.JERSEY}") {
    exclude group: 'junit', module: 'junit'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
  }
  testCompile "com.google.dagger:dagger:${Dependencies.DAGGER}"
  testCompile "org.mockito:mockito-core:${Dependencies.MOCKITO}"
  kaptTest "com.google.dagger:dagger-compiler:${Dependencies.DAGGER}"
}
