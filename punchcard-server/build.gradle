plugins {
  id 'application'
  id 'idea'
  id 'nebula.kotlin' version '1.2.60'
  id 'org.jetbrains.kotlin.kapt' version '1.2.60'
  id 'com.diffplug.gradle.spotless' version '3.14.0'
}

apply {
  from "$rootDir/gradle/kotlin.gradle"
  from "$rootDir/gradle/junit.gradle"
}

idea {
  module {
    sourceDirs += file('build/generated/source/kapt/main')
    testSourceDirs += file('build/generated/source/kapt/test')
    generatedSourceDirs += file('build/generated/source/kapt/main')
    generatedSourceDirs += file('build/generated/source/kapt/test')
  }
}

spotless {
  kotlin {
    ktlint()
  }
}

mainClassName = "com.github.ptrteixeira.punchcard.StravaPunchcardApplication"
distTar.compression = Compression.GZIP

startScripts {
  applicationName = "punchcard"
}

distributions {
  main {
    baseName = 'punchcard'
  }
}

dependencies {
  compile project(':kotlin-dropwizard-dsl')
  compile project(':sentry-appender')
  compile project(':strava-api')
  compile "io.dropwizard:dropwizard-core:${Dependencies.DROPWIZARD}"
  compile "io.sentry:sentry-logback:${Dependencies.SENTRY}"
  compile "io.micrometer:micrometer-core:${Dependencies.MICROMETER}"
  compile "io.micrometer:micrometer-registry-prometheus:${Dependencies.MICROMETER}"
  compile "com.fasterxml.jackson.module:jackson-module-kotlin:${Dependencies.JACKSON}"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${Dependencies.JACKSON}"
  compile "com.jakewharton.retrofit:retrofit2-reactor-adapter:${Dependencies.RETROFIT_REACTOR_ADAPTER}"
  compile "com.squareup.retrofit2:converter-jackson:${Dependencies.RETROFIT}"
  compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:${Dependencies.COROUTINES}"
  compile "org.jetbrains.kotlinx:kotlinx-coroutines-reactive:${Dependencies.COROUTINES}"
  compile "com.google.dagger:dagger:${Dependencies.DAGGER}"
  compile "com.tylerkindy:dropwizard-dagger:${Dependencies.DROPWIZARD_DAGGER}"
  kapt "com.google.dagger:dagger-compiler:${Dependencies.DAGGER}"

  testCompile "org.assertj:assertj-core:${Dependencies.ASSERTJ}"
  testCompile "io.dropwizard:dropwizard-testing:${Dependencies.DROPWIZARD}"
  testCompile("org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:${Dependencies.JERSEY}") {
    exclude group: 'junit', module: 'junit'
    exclude group: 'javax.servlet', module: 'javax.servlet-api'
  }
  testCompile "org.mockito:mockito-core:${Dependencies.MOCKITO}"
  kaptTest "com.google.dagger:dagger-compiler:${Dependencies.DAGGER}"
}
